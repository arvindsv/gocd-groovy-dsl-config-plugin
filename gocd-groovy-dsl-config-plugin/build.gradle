/*
 * Copyright 2018 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'groovy'

configurations {
  extractedAtTopLevel
}

dependencies {
  compile project(':dsl')
  compile project(':cli')
  compile project(':groovy-export')

  compile group: 'org.codehaus.groovy', name: 'groovy', version: groovyVersion

  extractedAtTopLevel project(':jar-class-loader')

  compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
  compile group: 'org.apache.ant', name: 'ant', version: antVersion
  compile group: 'org.apache.commons', name: 'commons-lang3', version: commonsLangVersion
  runtime group: 'org.hibernate.validator', name: 'hibernate-validator-annotation-processor', version: hiberVersion

  compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: pluginApiVersion

  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jupiterVersion
  testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: jupiterVersion
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: jupiterVersion

  testCompile group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
  testCompile group: 'org.assertj', name: 'assertj-core', version: assertJVersion
  testCompile group: 'net.javacrumbs.json-unit', name: 'json-unit-fluent', version: jsonFluentVersion
}

sourceSets {
  main.compileClasspath += project.configurations.compileOnly
  main {
    java { srcDirs = [] }
    groovy { srcDirs += ['src/main/java'] }
  }
}

jar {
  dependsOn project.configurations.extractedAtTopLevel

  manifest {
    attributes 'Main-Class': 'com.thoughtworks.gocd.Boot'
    attributes 'GoCD-Main-Class': 'cd.go.contrib.plugins.configrepo.groovy.cli.Main'
  }

  from(configurations.compile) {
    into "lib/"
  }

//    This is useful for debugging
  from(sourceSets.main.java) {
    into "/"
  }

  from(project.configurations.extractedAtTopLevel.collect { it.isDirectory() ? it : zipTree(it) }) {
    into("/")
  }
}

test {
  useJUnitPlatform()
}